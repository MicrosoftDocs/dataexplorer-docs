---
title:  "ipv6_lookup plugin"
description: Learn how to use the ipv6_lookup plugin to look up an IPv6 address in a lookup table.
ms.reviewer: alexans
ms.topic: reference
ms.date: 07/09/2024
monikerRange: "azure-data-explorer || microsoft-fabric"
---
# ipv6_lookup plugin

> [!INCLUDE [applies](../includes/applies-to-version/applies.md)] [!INCLUDE [fabric](../includes/applies-to-version/fabric.md)] [!INCLUDE [azure-data-explorer](../includes/applies-to-version/azure-data-explorer.md)]

The `ipv6_lookup` plugin looks up an IPv6 value in a lookup table and returns rows with matched values. The plugin is invoked with the [`evaluate`](evaluate-operator.md) operator.

## Syntax

*T* `|` `evaluate` `ipv6_lookup(` *LookupTable* `,` *SourceIPv6Key* `,` *IPv6LookupKey* [`,` *return_unmatched* ] `)`

## Parameters

| Name | Type | Required | Description |
|---|---|---|---|
| *T* | `string` |  :heavy_check_mark: | The tabular input whose column *SourceIPv6Key* is used for IPv6 matching.|
| *LookupTable* | `string` |  :heavy_check_mark: | Table or tabular expression with IPv6 lookup data, whose column *LookupKey* is used for IPv6 matching. IPv6 values can be masked using [IP-prefix notation](#ip-prefix-notation).|
| *SourceIPv6Key* | `string` |  :heavy_check_mark: | The column of *T* with IPv6 string to be looked up in *LookupTable*. IPv6 values can be masked using [IP-prefix notation](#ip-prefix-notation).|
| *IPv6LookupKey* | `string` |  :heavy_check_mark: | The column of *LookupTable* with IPv6 string that is matched against each *SourceIPv6Key* value.|
| *return_unmatched* | `bool` | | A boolean flag that defines if the result should include all or only matching rows (default: `false` - only matching rows returned).|

[!INCLUDE [ip-prefix-notation](../includes/ip-prefix-notation.md)]

## Returns

The `ipv6_lookup` plugin returns a result of join (lookup) based on IPv6 key. The schema of the table is the union of the source table and the lookup table, similar to the result of the [`lookup` operator](lookup-operator.md).

If the *return_unmatched* argument is set to `true`, the resulting table includes both matched and unmatched rows (filled with nulls).

If the *return_unmatched* argument is set to `false`, or omitted (the default value of `false` is used), the resulting table has as many records as matching results. This variant of lookup has better performance compared to `return_unmatched=true` execution.

> [!NOTE]
>
> * This plugin covers the scenario of IPv6-based join, assuming a small lookup table size (100K-300K rows), with the input table optionally having a larger size.
> * The performance of the plugin will depend on the sizes of the lookup and data source tables, the number of columns, and number of matching records.

## Examples

### IPv6 lookup - matching rows only

```kusto
// IP lookup table: IP_Data (the data is generated by ChatGPT).
let IP_Data = datatable(network:string, continent_code:string ,continent_name:string, country_iso_code:string, country_name:string)
[
  "2001:0db8:85a3::/48","NA","North America","US","United States",
  "2404:6800:4001::/48","AS","Asia","JP","Japan",
  "2a00:1450:4001::/48","EU","Europe","DE","Germany",
  "2800:3f0:4001::/48","SA","South America","BR","Brazil",
  "2c0f:fb50:4001::/48","AF","Africa","ZA","South Africa",
  "2607:f8b0:4001::/48","NA","North America","CA","Canada",
  "2a02:26f0:4001::/48","EU","Europe","FR","France",
  "2400:cb00:4001::/48","AS","Asia","IN","India",
  "2801:0db8:85a3::/48","SA","South America","AR","Argentina",
  "2a03:2880:4001::/48","EU","Europe","GB","United Kingdom"
];
let IPs = datatable(ip:string)
[
  "2001:0db8:85a3:0000:0000:8a2e:0370:7334", // United States
  "2404:6800:4001:0001:0000:8a2e:0370:7334", // Japan
  "2a02:26f0:4001:0006:0000:8a2e:0370:7334", // France
  "a5e:f127:8a9d:146d:e102:b5d3:c755:abcd",  // N/A
  "a5e:f127:8a9d:146d:e102:b5d3:c755:abce"   // N/A
];
IPs
| evaluate ipv6_lookup(IP_Data, ip, network)
```

**Output**

|network|continent_code|continent_name|country_iso_code|country_name|ip|
|---|---|---|---|---|---|
|2001:0db8:85a3::/48|NA|North America|US|United States|2001:0db8:85a3:0000:0000:8a2e:0370:7334|
|2404:6800:4001::/48|AS|Asia|JP|Japan|2404:6800:4001:0001:0000:8a2e:0370:7334|
|2a02:26f0:4001::/48|EU|Europe|FR|France|2a02:26f0:4001:0006:0000:8a2e:0370:7334|

### IPv6 lookup - return both matching and nonmatching rows

```kusto
// IP lookup table: IP_Data (the data is generated by ChatGPT).
let IP_Data = datatable(network:string, continent_code:string ,continent_name:string, country_iso_code:string, country_name:string)
[
  "2001:0db8:85a3::/48","NA","North America","US","United States",
  "2404:6800:4001::/48","AS","Asia","JP","Japan",
  "2a00:1450:4001::/48","EU","Europe","DE","Germany",
  "2800:3f0:4001::/48","SA","South America","BR","Brazil",
  "2c0f:fb50:4001::/48","AF","Africa","ZA","South Africa",
  "2607:f8b0:4001::/48","NA","North America","CA","Canada",
  "2a02:26f0:4001::/48","EU","Europe","FR","France",
  "2400:cb00:4001::/48","AS","Asia","IN","India",
  "2801:0db8:85a3::/48","SA","South America","AR","Argentina",
  "2a03:2880:4001::/48","EU","Europe","GB","United Kingdom"
];
let IPs = datatable(ip:string)
[
  "2001:0db8:85a3:0000:0000:8a2e:0370:7334", // United States
  "2404:6800:4001:0001:0000:8a2e:0370:7334", // Japan
  "2a02:26f0:4001:0006:0000:8a2e:0370:7334", // France
  "a5e:f127:8a9d:146d:e102:b5d3:c755:abcd",  // N/A
  "a5e:f127:8a9d:146d:e102:b5d3:c755:abce"   // N/A
];
IPs
| evaluate ipv6_lookup(IP_Data, ip, network, true)
```

**Output**

|network|continent_code|continent_name|country_iso_code|country_name|ip|
|---|---|---|---|---|---|
|2001:0db8:85a3::/48|NA|North America|US|United States|2001:0db8:85a3:0000:0000:8a2e:0370:7334|
|2404:6800:4001::/48|AS|Asia|JP|Japan|2404:6800:4001:0001:0000:8a2e:0370:7334|
|2a02:26f0:4001::/48|EU|Europe|FR|France|2a02:26f0:4001:0006:0000:8a2e:0370:7334|
|                   |  |      |  |      |a5e:f127:8a9d:146d:e102:b5d3:c755:abcd|
|                   |  |      |  |      |a5e:f127:8a9d:146d:e102:b5d3:c755:abce|


## Related content

* Overview of [IPv6/IPv6 functions](scalar-functions.md#ipv4ipv6-functions)
